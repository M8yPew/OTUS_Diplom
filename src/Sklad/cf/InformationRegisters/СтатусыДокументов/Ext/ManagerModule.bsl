#Область Брокер

Процедура ЗагрузитьСтатусы(ДанныеСообщения) Экспорт
	
	Записи = ДанныеСообщения.Записи;
	
	Для Каждого Запись Из Записи Цикл
		
		Документы.ЗаказНаСборку.ЗагрузитьДокумент(Запись.Документ);
		
		Ссылка = Документы.ЗаказНаСборку.ПолучитьСсылку(Новый УникальныйИдентификатор(Запись.Документ.Реквизиты.Идентификатор));
		
		СоответствиеСтатусов = СоответствиеСтатусовЗаказовКлиентаЗаказамНаСборку();
		Статус = СоответствиеСтатусов[Запись.Статус.Реквизиты.Имя];
		
		Если Статус <> Неопределено Тогда
		
			НоваяЗапись = РегистрыСведений.СтатусыДокументов.СоздатьМенеджерЗаписи();
			НоваяЗапись.Документ 	= Ссылка;
			НоваяЗапись.Статус 		= Статус;
			НоваяЗапись.Записать();
		
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВыгружаемыеРеквизиты() Экспорт

	Результат = Новый Массив();
	
	Результат.Добавить("Документ");
	Результат.Добавить("Статус");
	
	Возврат Результат;	
	
КонецФункции

Функция ДанныеОбъектаКВыгрузке(Объект) Экспорт
	
	ПолноеИмя = Объект.Метаданные().ПолноеИмя();
	ДанныеОбъекта = ОбменСообщениямиПодготовкаСообщенияСервер.ШаблонСообщенияДляПреобразованияОбъектаВJSON();
    ДанныеОбъекта.ПолноеИмяОбъектаМетаданных = ПолноеИмя;
	
	Записи = Новый Массив();
	
	// Так как регистр может содержать много записей
	Для Каждого Запись Из Объект Цикл
		
		ВыгружаемыеСтатусы = Перечисления.СтатусыЗаказовНаСборку.ВыгружаемыеСтатусы();
		
		Если ВыгружаемыеСтатусы.Найти(Запись.Статус) <> Неопределено Тогда 
		
			Результат = ВыгрузитьЗаписьИзНабораЗаписей(Запись, ПолноеИмя);
			Записи.Добавить(Результат);
		
		КонецЕсли;
		
	КонецЦикла;		
	
	Если Записи.Количество() > 0 Тогда
	
		ДанныеОбъекта.Записи = Записи;
		
	Иначе
		
		ДанныеОбъекта = Неопределено;
		
	КонецЕсли;
	
	Возврат ДанныеОбъекта;
	
КонецФункции 

Функция ВыгрузитьЗаписьИзНабораЗаписей(ЗаписьВРегистре, ПолноеИмя)
	
	ВыгружаемыеРеквизиты = ВыгружаемыеРеквизиты();	
		
	ДанныеЗаписи = Новый Структура();
	
	Для Каждого ВыгружаемыйРеквизит Из ВыгружаемыеРеквизиты Цикл
		
		Значение = ЗаписьВРегистре[ВыгружаемыйРеквизит];
		Тип = ТипЗнч(Значение);
		
		Если ОбщегоНазначения.ЭтоСсылка(Тип) Тогда 
			ПолноеИмяОбъекта 	= Значение.Метаданные().ПолноеИмя();
			МенеджерОбъекта 	= ОбменСообщениямиСлужебный.МенеджерОбъектаПоПолномуИмени(ПолноеИмяОбъекта); 
			ДанныеОбъекта 		= МенеджерОбъекта.ДанныеОбъектаКВыгрузке(Значение);
			ДанныеЗаписи.Вставить(ВыгружаемыйРеквизит, ДанныеОбъекта);
		Иначе			
			ДанныеЗаписи.Вставить(ВыгружаемыйРеквизит, Значение);	
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат ДанныеЗаписи;
	
КонецФункции


Функция СоответствиеСтатусовЗаказовКлиентаЗаказамНаСборку() 

	Соответствие = Новый Соответствие;
	
	Соответствие.Вставить("Отгрузить", Перечисления.СтатусыЗаказовНаСборку.Отгрузить);
	
	Возврат Соответствие;
	
КонецФункции

#КонецОбласти