 #Область ПрограммныйИнтерфейс

Функция СтатусЗаказаНаСборку(ЗаказНаСборку) Экспорт

	Результат = Перечисления.СтатусыЗаказовНаСборку.ПустаяСсылка();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатусыДокументов.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.СтатусыДокументов КАК СтатусыДокументов
	|ГДЕ
	|	СтатусыДокументов.Документ = &Документ";
	
	Запрос.УстановитьПараметр("Документ", ЗаказНаСборку);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ВыборкаДетальныеЗаписи.Следующий();
		
		Результат = ВыборкаДетальныеЗаписи.Статус;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

Процедура ОбновитьСтатусЗаказаНаСборку(ЗаказНаСборку, НовыйСтатус) Экспорт

	Если ТипЗнч(НовыйСтатус) = Тип("Строка") Тогда	
		Статус = Перечисления.СтатусыЗаказовНаСборку[НовыйСтатус];
	Иначе
		Статус = НовыйСтатус;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.СтатусыДокументов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Документ = ЗаказНаСборку;
	МенеджерЗаписи.Статус = Статус;
	МенеджерЗаписи.Записать();	
	
КонецПроцедуры

#КонецОбласти